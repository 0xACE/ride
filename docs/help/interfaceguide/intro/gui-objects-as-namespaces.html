<!doctype html>
<html>
<head><meta charset="utf-8"><title></title><link rel="stylesheet" href="../../../help.css"></head>
<body><h2><a name="GUI_Namespaces"></a>GUI Objects as Namespaces</h2><p>GUI objects are a special type of namespace and this has several useful implications. Firstly, instead of creating the children of an object from <span class="Italic">outside</span> in the workspace, you can use <tt>)CS</tt> to change to an object and create them from within. The only restriction is that you can only create GUI objects that are valid as children of the current object. A second benefit is that you can put the callback functions, together with any global variables they require, into the objects to which they apply. Consider the following example.</p><p>First make a Form <tt>F1</tt></p><pre>      'F1' ⎕WC 'Form' 'GUI Objects as Namespaces'
                      ('Size' 25 50)</pre><p>Then change to the Form’s namespace</p><pre>      )CS F1
#.F1
</pre><p>Now you can create a Group (or any other child object), but because you are already <span class="Italic">inside</span> the Form, the name you give to the Group will be taken as being <span class="Italic">relative</span> to the Form. In other words, you must specify the part of the name that applies to the Group itself, leaving out the <tt>'F1.'</tt> prefix that you would use if you executed the statement <span class="Italic">outside</span> in the workspace.</p><pre>      'CH' ⎕WC 'Group' 'Counter' (10 10)( 70 60)</pre><p>You can continue to create other objects </p><pre>      'OK'  ⎕WC 'Button' '&amp;Ok' (20 80)(⍬ 15)
      'CAN' ⎕WC 'Button' '&amp;Cancel' (60 80) (⍬ 15)
      'FNT' ⎕WC 'Font' 'Arial' 16 ('Weight' 700)</pre><p>If you ask for a list of objects, you will see only those within the current namespace</p><pre>      )OBJECTS
CAN     CH      FNT     OK</pre><p>When you are inside an object you can also set (or get) a property directly, so you can set the FontObj property for the Form with the following statement.</p><pre>      FontObj←'FNT'</pre><p>You can achieve the same with <tt>⎕WS</tt> by omitting its left argument:</p><pre>      ⎕WS 'FontObj' 'FNT'</pre><p>You can create a child of the Group from outside it ...</p><pre>      'CH.UP' ⎕WC 'Button' '+1' (20 10)(30 20)</pre><p>or you can change to it and create others from within...</p><pre>      )CS CH
#.F1.CH
      'DOWN' ⎕WC 'Button' '-1' (60 10)(30 20)
      'FNT' ⎕WC 'Font' 'Arial' 32
      'CTR' ⎕WC 'Label' ('FieldType' 'Numeric' )
                ('FontObj' 'FNT') </pre><p>Once again, if you request a list of objects you will see only those in the current namespace.</p><pre>      )OBJECTS
CTR     DOWN    FNT     UP</pre><p>You can create functions and variables in a GUI namespace in exactly the same way as in any other. So, for example, you could create a variable called <tt>COUNT</tt> and a function <tt>CHANGE</tt> to update it:</p><pre>      COUNT ← 0
 
     ∇ INCR CHANGE MSG
[1]    COUNT←COUNT+INCR
[2]    CTR.Value←COUNT
     ∇</pre><p>You can also make <tt>CHANGE</tt> a callback function for the two Buttons.</p><pre>      UP.onSelect←'CHANGE' 1
      DOWN.onSelect←'CHANGE' ¯1</pre><p>Notice that because you were in the <tt>F1.CH</tt> namespace when you made this association, the event will fire the function <tt>CHANGE</tt> in the <tt>F1.CH</tt> namespace and, furthermore, it will execute it within that namespace. Hence the names referenced by the function are the local names, i.e. the variable <tt>COUNT</tt> and the Label <tt>CTR</tt>, within that namespace. </p><p>So if you now switch back to the outer workspace</p><pre>      )CS
#</pre><p>and click on the buttons...</p><p>The result will appear approximately as shown below</p><p><img src="../images/gui-objects-as-namespaces.png" /></p><h4><a name="Attaching_GUI"></a>Attaching GUI Objects to Namespaces</h4><p>Monadic <tt>⎕WC</tt> is used to <span class="Italic">attach</span> a GUI component to an existing object. The existing object must be a pure namespace or an appropriate GUI object (one that can legitimately be placed at that point in the object hierarchy). The operation may be performed by changing space to the object or by running <tt>⎕WC </tt><span class="Italic">inside</span> the object using the <span class="Italic">dot</span> syntax. For example, the following statements are equivalent.</p><pre>      )CS F
#.F
      ⎕WC 'Form'  ⍝ Attach a Form to this namespace

      )CS
#
      F.⎕WC'Form' ⍝ Attach a Form to namespace F</pre><p>Monadic <tt>⎕WC</tt> is often used in conjunction with the KeepOnClose property. This property specifies whether or not an object remains in existence when its parent Form (or in the case of a Form, the Form itself) is closed by the user or receives a Close event.</p><p>This facility is particularly useful if you wish to have functions and variables encapsulated within your Forms. You may want to save these structures in your workspace, but you do not necessarily want the Forms to be visible when the workspace is loaded.</p><p>An alternative way to achieve this is to prevent the user from closing the Form and instead make it invisible. This is achieved by intercepting the Close event on the Form and set its Visible property to 0. Then, when the Form is subsequently required, its Visible property is set back to 1. However, if the Form needs adjustment because the workspace was loaded on a PC with different screen resolution or for other reasons, it may not be easy to achieve the desired result using <tt>⎕WS</tt>. Monadic <tt>⎕WC</tt> is generally a better solution.</p><h4><a name="GUI_Refs"></a>Namespace References and GUI Objects</h4><p>The use of a GUI name in an expression is a reference to the GUI object, or <span class="Italic">ref</span> for short. If you assign a <span class="Italic">ref</span> or call a function with a <span class="Italic">ref</span> as an argument, the <span class="Italic">reference</span> to the GUI object is copied, not the GUI object itself.</p><p>So for example, if you have a Form named <tt>F</tt>:</p><pre>      'F'⎕WC 'Form' </pre><p>Assigning <tt>F</tt> to <tt>F1</tt>, does not create a second Form <tt>F1</tt>; it  simply creates a second reference (<tt>F1</tt>) to the Form <tt>F</tt>.  Subsequently, you can manipulate the Form <tt>F</tt> using either <tt>F</tt> or <tt>F1</tt>.</p><pre> 
      F1←F
      F1
#.F
      F1.Caption←'Hello World'
      F.Caption
Hello World</pre><p>Similarly, if you call a function with <tt>F</tt> as the argument, the local argument name becomes a second reference to the Form, and a new Form is not created:</p><p>Here is a simple function which approximately centres a Form in the middle of the screen:</p><pre>     ∇ R←SHOW_CENTRE FORM;OLD;SCREEN
[1]    SCREEN←⊃'.'⎕WG'DevCaps'
[2]    OLD←FORM.Coord
[3]    FORM.Coord←'Pixel'
[4]    R←FORM.Posn←⌊0.5×SCREEN-FORM.Size
[5]    FORM.Coord←OLD
     ∇</pre><p>The function can be called using either <tt>F</tt> or <tt>F1</tt> (or any other Form) as an argument:</p><pre>      SHOW_CENTRE F
287 329
      SHOW_CENTRE F1
287 329</pre><p>A <span class="Italic">ref</span> to a GUI object can conveniently be used as the argument to <tt>:With</tt>; for example, the <tt>SHOW_CENTRE</tt> function can instead be written as follows:</p><pre>     ∇ R←SHOW_CENTRE FORM;OLD;SCREEN
[1]    SCREEN←⊃'.'⎕WG'DevCaps'
[2]    :With FORM
[3]        OLD←Coord
[4]        Coord←'Pixel'
[5]        R←Posn←⌊0.5×SCREEN-Size
[6]        Coord←OLD
[7]    :EndWith
     ∇</pre><p>If instead, you actually want to duplicate (clone) a GUI object, you may do so by calling <tt>⎕WC</tt> with a <span class="Italic">ref</span> as the right argument and the new name as the left argument.</p><p>For example:</p><pre>    'F' ⎕WC 'Form' 'Cloning Example'
    'F.B' ⎕WC 'Group' 'Background' (10 10)(80 30)
    'F.B.R' ⎕WC 'Button' 'Red' (20 10)('Style' 'Radio')
    'F.B.B' ⎕WC 'Button' 'Blue' (50 10)('Style' 'Radio')
    'F.B.G' ⎕WC 'Button' 'Green' (80 10)('Style' 'Radio')</pre><p>Then, instead of creating a second Group for selecting Foreground colour line by line as before, you can clone the "Background" Group as follows:</p><pre>      'F.F' ⎕WC F.B</pre><p>The new Group <tt>F.F</tt> is an exact copy of <tt>F.B</tt> and will have the same Posn, Size and Caption, as well as having identical children. To achieve the desired result, it is therefore only necessary to change its Posn and Caption properties; for example:</p><pre>      F.F.Caption F.F.Posn ← 'ForeGround' (10 60)</pre><p>The result is illustrated below.</p><p><img src="../images/cloning-example.png" /></p><p>Note that when a namespace is cloned in this way, the objects (functions, variables and other namespaces) within it are not necessarily duplicated. Instead, the objects in cloned namespaces are in effect just pointers to the original objects. However, if you subsequently change the clone, or the original object to which it refers, the two are de-coupled and a second <span class="Italic">copy</span> ensues. This mechanism makes it possible to create large numbers of <span class="Italic">instances</span> of a single <span class="Italic">class</span> namespace without consuming an excessive amount of workspace.</p></body>
</html>