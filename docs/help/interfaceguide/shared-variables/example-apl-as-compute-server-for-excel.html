<!doctype html>
<html>
<head><meta charset="utf-8"><title></title><link rel="stylesheet" href="../../../help.css"></head>
<body><h2><a name="APL_Compute_Server"></a>Example : APL as Compute Server for Excel</h2><p>The following instructions illustrate how APL can act as a "compute server" for Microsoft Excel, using two shared variables. One variable is used to read the data from Excel; the other is used to pass back the result.</p><p>Start Excel and enter some NUMBERS into the cells R1C1 to R3C3 of the spreadsheet "SHEET1".</p><p>Start Dyalog APL/W and size your windows so that both the Excel window and the APL Session window can be viewed comfortably at the same time.</p><p><tt>)LOAD</tt> the EXCEL workspace. This contains the following functions :</p><pre>     ∇ RUN;Z;⎕WSID
[1]    Z←'DDE:EXCEL|SHEET1'⎕SVO 'DATA R1C1:R3C3'
[2]    →(2=Z)/L1
[3]    'No Excel out there ?' ⋄ →0
[4]   L1:
[5]    CALC
[6]    ⎕WSID←'EXCEL'
[7]    Z←'DDE:'⎕SVO 'RESULT ANSWER'
[8]    'Now type "=dyalog|excel!answer" into'
[9]    'cell A4 in your spreadsheet'
[10]  L2:⎕DL 1
[11]   →(2≠⎕SVO 'RESULT')/L2 ⍝Wait for Excel to connect
[12]   'Connected ...'
[13]   '.'⎕WS 'EVENT' 50 'CALLB'
[14]  ⎕DQ '.'
     ∇
</pre><pre>
     ∇ CALLB MSG
[1]   ⍝ Callback to recalculate when Excel changes DATA
[2]    →(0 0 1 1≡⎕SVS 'DATA')/0
[3]    CALC
     ∇
 
     ∇ CALC;⎕TRAP
[1]    ⎕TRAP←0 'C' '→ERR'
[2]    RESULT←+/,DATA
[3]    →0
[4]   ERR:RESULT←⊂⎕EM ⎕EN
     ∇</pre><p>Type the following statement in the APL Session :</p><pre>      RUN</pre><p>Now type "=dyalog|excel!answer" into cell A4 in your spreadsheet</p><p>Follow the above instructions to establish a link from APL to cell A4 in your Excel spreadsheet. The result of the computation will be displayed.</p><p>Try changing some of the numbers in the spreadsheet and watch as APL re-calculates the sum.</p><p>Try entering a character string in cell A1. Note that APL sends back a character string containing <tt>DOMAIN ERROR</tt>.</p><p>Use Ctrl+Break or select "Interrupt" from the <span class="Italic">Action</span> menu in the Session window to stop <tt>⎕DQ</tt>.</p><script src="../../../help.js"></script></body>
</html>