<!doctype html>
<html>
<head><meta charset="utf-8"><title></title><link rel="stylesheet" href="../../../help.css"></head>
<body><h2><a name="Signalling_Events"></a>Signalling Events</h2><p>It would be useful to be able to employ the idea of cutting back the stack and taking an alternative route through the code, when a condition other than an APL error occurs. To achieve this, we must be able to trap on errors other than APL errors, and we must be able to define these errors to APL. We do the former by using error codes in the range 500 to 999, and the latter by using <tt>⎕SIGNAL</tt>.</p><p>Consider our system; ideally, when an unexpected error occurs, we want to save a snapshot copy of our workspace (execute <tt>BUG</tt> in place), then immediately jump back to <tt>REPORT</tt> and reduce our options. We can achieve this by changing our functions a little, and using <tt>⎕SIGNAL</tt>:</p><pre>      ∇ REPORT;OPTIONS;OPTION;⎕TRAP
   [1] ⍝ Driver functions for report sub-system. If an
   [2] ⍝ unexpected error occurs, make a snapshot copy
   [3] ⍝ of the workspace, then cutback the stack to
   [4] ⍝ this function, reduce the option list &amp; resume
   [5] ⍝ Set global trap
   [6]  ⎕TRAP←(500 'C' '→ERR')(0 'E' 'BUG')
   [7] ⍝ Available options
   [8]  OPTIONS←'REP1' 'REP2' 'REP3' 'REP4'
   [9] ⍝ Ask user to choose
  [10] LOOP:→END×⍳0=⍴OPTION←MENU OPTIONS
  [11] ⍝ Execute relevant function
  [12]  ⍎OPTION
  [13] ⍝ Repeat until EXIT
  [14]  →LOOP
  [15] ⍝ Tell user ...
  [16] ERR:MESSAGE'Unexpected error in',OPTION
  [17] ⍝ ... what's happening
  [18]  MESSAGE'Removing from list'
  [19] ⍝ Remove option from list
  [20]  OPTIONS←OPTIONS~⊂OPTION
  [21] ⍝ And repeat
  [22]  →LOOP
  [23] ⍝ End
  [24] END:</pre><p class="pagebreakafter"> </p><pre>       ∇ BUG
   [1] ⍝ Called whenever there is an unexpected error
   [2]   '*** UNEXPECTED ERROR OCCURRED IN: ',⊃1↓⎕SI
   [3]   '*** PLEASE CALL YOUR SYSTEM ADMINISTRATOR'
   [4]   '*** WORKSPACE SAVED AS BUG.',⊃1↓⎕SI
   [5]   ⍝ Tidy up ... reset ⎕LX, untie files ... etc
   [6]   ⎕SAVE 'BUG.',⊃1↓⎕SI
   [7]   '*** RETURNING TO DRIVER FOR RESELECTION'
   [8]   ⎕SIGNAL 500
       ∇</pre><p>Now when the unexpected error occurs, the first trap specification catches it, and the <tt>BUG</tt> function is executed in place. Instead of logging the user off as before, an <tt>error 500</tt> is signalled to APL. APL checks its trap specifications, sees that 500 has been set in <tt>REPORT</tt> as a cut-back, so cuts back to <tt>REPORT</tt> before branching to <tt>ERR</tt>.</p><h4><a name="Flow_Control"></a>Flow Control</h4><p>Error handling, which employs a combination of all the system functions and variables described, allows us to dynamically alter the flow of control through our system, as well as allow us to handle errors gracefully. It is a very powerful facility, which is simple to use, but is often neglected.</p></body>
</html>