<!doctype html>
<html>
<head><meta charset="utf-8"><title></title><link rel="stylesheet" href="../../../../help.css"></head>
<body><h4>Example 3</h4><p>This example, uses APL code to both build the user-interface (instead of using XAML) and handle the data binding. In this case both the Text and the FontSize properties are bound to APL variables. The function is shown below:</p><pre>     ∇ TextFontSize(txt size);⎕USING;win;sink
[1]    ⎕USING←'System'
[2]    ⎕USING,←,⊂'System.Windows.Controls,WPF/PresentationFramework.dll'
[3]    ⎕USING,←⊂'System.Windows.Controls.Primitives,WPF/PresentationFramework.dll'
[4]    ⎕USING,←⊂'System.Windows,WPF/PresentationFramework.dll'
[5]    ⎕USING,←⊂'System.Windows,WPF/PresentationCore.dll'
[6]
[7]   ⍝ Create a Window, DockPanel and TextBox
[8]    win←⎕NEW Window
[9]    win.SizeToContent←SizeToContent.WidthAndHeight
[10]   win.Title←'Data Binding (Text and FontSize)'
[11]   win.txtBox←⎕NEW TextBox
[12]   win.txtBox.Width←350
[13]   win.Content←win.txtBox
[14]
[15]  ⍝ Define data binding from variable "txtSource"
[16]  ⍝ to the Text property of TextBox win.txtBox
[17]   ⎕EX'txtSource'
[18]   txtSource←txt
[19]   win.txtbinding←⎕NEW Data.Binding(⊂'txtSource')
[20]   win.txtbinding.Source←2015⌶'txtSource'
[21]   win.txtbinding.Mode←Data.BindingMode.TwoWay
[22]   win.txtbinding.UpdateSourceTrigger←Data.UpdateSourceTrigger.PropertyChanged
[23]   sink←win.txtBox.SetBinding TextBox.TextProperty win.txtbinding
[24]
[25]  ⍝ Define data binding from variable "sizeSource"
[26]  ⍝ to the FontSize property of TextBox win.txtBox
[27]   ⎕EX'sizeSource'
[28]   sizeSource←size
[29]   win.fntbinding←⎕NEW Data.Binding(⊂'sizeSource')
[30]   win.fntbinding.Source←Int32(2015⌶)'sizeSource'
[31]   win.fntbinding.Mode←Data.BindingMode.OneWay
[32]   sink←win.txtBox.SetBinding TextBox.FontSizeProperty win.fntbinding
[33]
[34]   win.Show
     ∇
</pre><p>Apart from the code that creates the controls, the only material difference between this and the previous examples is the way that the bindings are handled.</p><p>In code (as opposed to using XAML) this is done using explicit <span class="NetType">Binding</span> objects<span class="MCTextPopup"><span class="MCTextPopupBody_0" style="display: none; ">Binding objects are implicit in all binding operations, but are created declaratively when using XAML.</span><a href="javascript:void(0);" class="MCTextPopupSpot_0" onclick="FMCTextPopup( event, this ); return false;" style="font-size: 0.9em; vertical-align: super">1</a></span>. The code for binding the <span class="Member">Text</span> property to the <tt>txtSource</tt> variable is as follows:</p><pre>
[19]   win.txtbinding←⎕NEW Data.Binding(⊂'txtSource')
[20]   win.txtbinding.Source←2015⌶'txtSource'
[21]   win.txtbinding.Mode←Data.BindingMode.TwoWay
[22]   win.txtbinding.UpdateSourceTrigger←Data.UpdateSourceTrigger.PropertyChanged
[23]   sink←win.txtBox.SetBinding TextBox.TextProperty win.txtbinding</pre><p>Line [19] creates a <span class="NetType">Binding</span> object, passing the constructor the  the name of the APL variable <tt>txtSource</tt> as the <span class="Member">Path</span> to the binding value.</p><pre>
[19]   win.txtbinding←⎕NEW Data.Binding(⊂'txtSource')</pre><p> Line [20] creates a Binding Source object using <tt>2015⌶</tt> as before, but this time assigns it to the <span class="Member">Source</span> property of the <span class="NetType">Binding</span> object. </p><pre>
[20]   win.txtbinding.Source←2015⌶'txtSource'</pre><p>Line [21] sets the <span class="Member">Mode</span> property of the <span class="NetType">Binding</span> object to <span class="Member">TwoWay</span> (a field of the <span class="NetType">BindingMode</span> Type). As in Example 1, this specifies two-way binding.</p><pre>
[21]   win.txtbinding.Mode←Data.BindingMode.TwoWay</pre><p>Line [22] sets the <span class="Member">UpdateSourceTrigger</span> property of the <span class="NetType">Binding</span> object to <span class="Member">PropertyChanged</span> (a field of the <span class="NetType">UpdateSourceTrigger</span> Type). This causes the value in the Binding Source (in this case <tt>txtSource</tt>) to be changed whenever the property (in this case the <span class="Member">Text</span> property) of the <span class="NetType">TextBox</span> changes. This will occur on every keystroke.</p><pre>
[22]   win.txtbinding.UpdateSourceTrigger←Data.UpdateSourceTrigger.PropertyChanged</pre><p> (Note that the three types <span class="NetType">Binding</span>, <span class="NetType">BindingMode</span> and<span class="NetType"> UpdateSourceTrigger</span> are located in <span class="NetType">System.Windows.Data</span>)</p><p>The code that establishes the binding between the <tt>sizeSource</tt> variable and the <span class="Member">FontSize</span> property is very similar.</p><p class="pagebreakafter"> </p><pre>[29]   win.fntbinding←⎕NEW Data.Binding(⊂'sizeSource')
[30]   win.fntbinding.Source←Int32(2015⌶)'sizeSource'
[31]   win.fntbinding.Mode←Data.BindingMode.OneWay
[32]   sink←win.txtBox.SetBinding
                TextBox.FontSizeProperty win.fntbinding</pre><p>Note however that (as in Example 2) the left-argument to <tt>(2015⌶)</tt> specifies that the exported data type of the <tt>sizeSource</tt> variable is to be <span class="NetType">Int32</span>.</p><h4>Testing the Data Binding</h4><pre>      )LOAD WPFIntro
      )CS DataBinding.TextFontSizeCode</pre><pre>      TextFontSize 'Hello World' 30</pre><p><img src="../../images/data-binding-text-fontsize-1.png" /></p><pre>      txtSource sizeSource←(⌽txtSource) 18</pre><p><img src="../../images/data-binding-text-fontsize-2.png" /></p><p>As in previous examples, when the user changes the text, the new text appears in <tt>txtSource</tt>.</p><p><img src="../../images/data-binding-text-fontsize-3.png" /></p><pre>      txtSource
Learn to play the bouzouki!
</pre><h4>Note</h4><p>It is perhaps worth mentioning that if you want to bind two properties <span class="Italic">of the same object</span> to two APL variables, it has to be done by writing code as shown in this example, using two separate Binding Source objects. This is because using XAML you may only associate a single Binding Source to an object.</p><p>However, this minor restriction is easily surmounted by using an APL namespace as a Binding Source as illustrated in the next Example.</p><script src="../../../../help.js"></script></body>
</html>