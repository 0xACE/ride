<!doctype html>
<html>
<head><meta charset="utf-8"><title></title><link rel="stylesheet" href="../../../../../help.css"></head>
<body><h3>Keyed Properties Example 1</h3><p>The Sparse2 Class illustrates the implementation and use of a Keyed Property.</p><p><tt>Sparse2</tt> represents a 2-dimensional sparse array each of whose dimensions are indexed by arbitrary character keys. The sparse array is implemented as a Keyed Property named <tt>Values</tt>. The following expressions show how it might be used.</p><pre>      SA1←⎕NEW Sparse2
      SA1.Values[⊂'Widgets';⊂'Jan']←100
      SA1.Values[⊂'Widgets';⊂'Jan']
100
      SA1.Values['Widgets' 'Grommets';'Jan' 'Mar' 'Oct']←10×2 3⍴⍳6
      SA1.Values['Widgets' 'Grommets';'Jan' 'Mar' 'Oct']
10 20 30
40 50 60
      SA1.Values[⊂'Widgets';'Jan' 'Oct']
10 30
      SA1.Values['Grommets' 'Widgets';⊂'Oct']
60
30</pre><h4>Sparse2 Class Example</h4><pre>:Class Sparse2  ⍝ 2D Sparse Array
    :Field Private keys
    :Field Private values
    ∇ make
      :Access Public
      :Implements Constructor
      keys←0⍴⊂'' ''
      values←⍬
    ∇
    :Property Keyed Values
    :Access Public Instance
        ∇ v←get arg;k
          k←arg.Indexers
          ⎕SIGNAL(2≠⍴k)/4
          k←fixkeys k
          v←(values,0)[keys⍳k]
        ∇
        ∇ set arg;new;k;v;n
          v←arg.NewValue
          k←arg.Indexers
          ⎕SIGNAL(2≠⍴k)/4
          k←fixkeys k
          v←(⍴k)(⍴⍣(⊃1=⍴,v))v
          ⎕SIGNAL((⍴k)≠⍴v)/5
          k v←,¨k v
          :If ∨/new←~k∊keys
              values,←new/v
              keys,←new/k
              k v/⍨←⊂~new
          :EndIf
          :If 0&lt;⍴k
              values[keys⍳k]←v
          :EndIf
        ∇
    :EndProperty
    
    ∇ k←fixkeys k
      k←(2≠≡¨k){,(⊂⍣⍺)⍵}¨k
      k←⊃(∘.{⊃,/⊂¨⍺ ⍵})/k
    ∇
:EndClass ⍝ 2D Sparse Array</pre><p>Internally, <tt>Sparse2</tt> maintains a list of keys and a list of values which are initialised to empty arrays by its constructor.</p><p>When an indexed assignment is made, the <tt>set</tt> function receives a list of keys (indices) in <tt>arg.Indexer</tt> and values in <tt>arg.NewValue</tt>. The function updates the values of existing keys, and adds new keys and their values to the internal lists.</p><p>When an indexed reference is made, the <tt>get</tt> function receives a list of keys (indices) in <tt>arg.Indexer</tt>. The function uses these keys to retrieve the corresponding values, inserting 0s for non-existent keys.</p><p>Note that in the expression:</p><pre> SA1.Values['Widgets' 'Grommets';'Jan' 'Mar' 'Oct']</pre><p>the structure of <tt>arg.Indexer</tt> is:</p><pre>.→-----------------------------------------------.
| .→---------------------. .→------------------. |
| | .→------. .→-------. | | .→--. .→--. .→--. | |
| | |Widgets| |Grommets| | | |Jan| |Mar| |Oct| | |
| | '-------' '--------' | | '---' '---' '---' | |
| '∊---------------------' '∊------------------' |
'∊-----------------------------------------------'</pre><script src="../../../../../help.js"></script></body>
</html>