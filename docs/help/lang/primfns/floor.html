<!doctype html>
<html>
<head><meta charset="utf-8"><title></title><link rel="stylesheet" href="../../../help.css"></head>
<body><h2><tt>R←⌊Y</tt>Floor</h2><p><tt>Y</tt> must be numeric.</p><p>For real numbers, <tt>R</tt> is the largest integer value less than or equal to <tt>Y</tt> within the comparison tolerance <tt>⎕CT</tt>.</p><h4>Examples</h4><pre>      ⌊¯2.3 0.1 100 3.3
¯3 0 100 3
 
      ⌊0.5 + 0.4 0.5 0.6
0 1 1</pre><p>For complex numbers, <tt>R</tt> depends on the relationship between the real and imaginary parts of the numbers in <tt>Y</tt>.</p><pre>      ⌊1j3.2 3.3j2.5 ¯3.3j¯2.5
1J3 3J2 ¯3J¯3</pre><p>The following (deliberately) simple function illustrates one way to express the rules for evaluating complex Floor.</p><pre>     ∇ fl←CpxFloor cpxs;a;b
[1]   ⍝ Complex floor of scalar complex number (a+ib)
[2]    a b←9 11○cpxs
[3]    :If 1&gt;(a-⌊a)+b-⌊b
[4]        fl←(⌊a)+0J1×⌊b
[5]    :Else
[6]        :If (a-⌊a)&lt;b-⌊b
[7]            fl←(⌊a)+0J1×1+⌊b
[8]        :Else
[9]            fl←(1+⌊a)+0J1×⌊b
[10]       :EndIf
[11]   :EndIf
     ∇
 
      CpxFloor¨1j3.2 3.3j2.5 ¯3.3j¯2.5
1J3 3J2 ¯3J¯3</pre><p><tt>⎕CT</tt> is an implicit argument of Floor.</p><script src="../../../help.js"></script></body>
</html>