<!doctype html>
<html>
<head><meta charset="utf-8"><title></title><link rel="stylesheet" href="../../../help.css"></head>
<body><h2><tt>R←⎕CR Y</tt>Canonical Representation </h2><p><tt>Y</tt> must be a simple character scalar or vector which represents the name of a defined function or operator.</p><p>If <tt>Y</tt> is a name of a defined function or operator, <tt>R</tt> is a simple character matrix.  The first row of <tt>R</tt> is the function or operator header.  Subsequent rows are lines of the function or operator.  <tt>R</tt> contains no unnecessary blanks, except for leading indentation of control structures, trailing blanks that pad each row, and the blanks in comments.  If <tt>Y</tt> is the name of a variable, a locked function or operator, an external function, or is undefined, <tt>R</tt> is an empty matrix whose shape is <tt>0 0</tt>.</p><h4>Example</h4><pre>      ∇R←MEAN X    ⍝ Arithmetic mean
[1]  R←(+/X)÷⍴X
[2]  ∇
      +F←⎕CR'MEAN'
R←MEAN X    ⍝ Arithmetic mean
R←(+/X)÷⍴X
 
      ⍴F
2 30</pre><p>The definition of <tt>⎕CR</tt> has been extended to names assigned to functions by specification (<tt>←</tt>), and to local names of functions used as operands to defined operators.</p><p>If <tt>Y</tt> is a name assigned to a primitive function, <tt>R</tt> is a one-element vector containing the corresponding function symbol.  If <tt>Y</tt> is a name assigned to a system function, <tt>R</tt> is a one element nested array containing the name of the system function.</p><h4>Examples</h4><pre>      PLUS←+
      +F←⎕CR'PLUS'
+
      ⍴F
1
      C←⎕CR
      C'C'
 ⎕CR
      ⍴C'C'
1</pre><p class="pagebreakafter"> </p><pre>
      ∇R←CONDITION (FN1 ELSE FN2) X
[1]   →CONDITION/L1
[2]   R←FN2 X ⋄ →0
[3]  L1:R←FN1 X
[4]   ∇
 
      2 ⎕STOP 'ELSE'
      (X≥0) ⌊ ELSE ⌈ X←¯2.5
 
ELSE[2]
       X
¯2.5
       ⎕CR'FN2'
⌈
       →⎕LC
¯2</pre><p>If <tt>Y</tt> is a name assigned to a derived function, <tt>R</tt> is a vector whose elements represent the arrays, functions, and operators from which <tt>Y</tt> was constructed.  Constituent functions are represented by their own <tt>⎕CR</tt>s, so in this respect the definition of <tt>⎕CR</tt> is recursive.  Primitive operators are treated like primitive functions, and are represented by their corresponding symbols.  Arrays are represented by themselves.</p><h4>Example</h4><pre>      BOX←2 2∘⍴
      +F←⎕CR'BOX'
 2 2 ∘⍴
      ⍴F
3
      ]display F
.→----------.
| .→--.     |
| |2 2| ∘ ⍴ |
| '~--' - - |
'∊----------'</pre><p>If <tt>Y</tt> is a name assigned to a defined function, <tt>R</tt> is the <tt>⎕CR</tt> of the defined function.  In particular, the name that appears in the function header is the name of the original defined function, not the assigned name <tt>Y</tt>.</p><h4>Example</h4><pre>      AVERAGE←MEAN
      ⎕CR'AVERAGE'
R←MEAN X    ⍝ Arithmetic mean
R←(+/X)÷⍴X</pre><script src="../../../help.js"></script></body>
</html>