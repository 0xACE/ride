<!doctype html>
<html>
<head><meta charset="utf-8"><title></title><link rel="stylesheet" href="../../../help.css"></head>
<body><h2><tt>{R}←⎕DF Y</tt>Display Form </h2><p><tt>⎕DF</tt> sets the <span class="Italic">Display Form </span>of a namespace, a GUI object, a Class, or an Instance of a Class.</p><p><tt>Y</tt> must be a simple character array that specifies the display form of a namespace. If defined, this array will be returned by the <span class="Italic">format</span> functions and <tt>⎕FMT</tt> instead of the default for the object in question. This also applies to the string that is displayed when the name is referenced but not assigned (the <span class="Italic">default display</span>).</p><p>The result <tt>R</tt> is the previous value of the Display Form which initially is <tt>⎕NULL</tt>.</p><pre>      'F'⎕WC'Form'
      ⍕F
#.F
      ⍴⍕F
3
      ⎕FMT F
#.F
      ⍴⎕FMT F
1 3
      F ⍝ default display uses ⍕
#.F
 
       F.⎕DF 'Pete''s Form'
       ⍕F
Pete's Form
       ⍴⍕F
11
       ⎕FMT F
Pete's Form
       ⍴⎕FMT F
1 11</pre><p>Notice that <tt>⎕DF</tt> will accept any character array, but <tt>⎕FMT</tt> always returns a matrix.</p><pre>      F.⎕DF 2 2 5⍴⎕A
      F
ABCDE
FGHIJ
     
KLMNO
PQRST
      ⍴⍕F
2 2 5
</pre><p class="pagebreakafter"> </p><pre>      ⍴⎕←⎕FMT F
ABCDE
FGHIJ
     
KLMNO
PQRST
5 5</pre><p>Note that <tt>⎕DF</tt> defines the Display Form statically, rather than dynamically.</p><pre>      'F'⎕WC'Form' 'This is the Caption'
      F
#.F
 
      F.(⎕DF Caption)⍝ set display form to <b>current</b> caption
      F
This is the Caption
 
      F.Caption←'New Caption' ⍝ changing caption does not
                              ⍝ change the display form
      F
This is the Caption</pre><p>You may use the Constructor function to assign the Display Form to an Instance of a Class. For example:</p><pre>:Class MyClass
    ∇ Make arg
      :Access Public
      :Implements Constructor
      ⎕DF arg
    ∇
:EndClass ⍝ MyClass
 
      PD←⎕NEW MyClass 'Pete'
      PD
Pete</pre><p>It is possible to set the Display Form for the Root and for <tt>⎕SE</tt></p><pre>      )CLEAR
clear ws
      #
#
      ⎕DF ⎕WSID
      #
CLEAR WS
 
      ⎕SE
⎕SE
      ⎕SE.⎕DF 'Session'
      ⎕SE
Session</pre><p>Note that <tt>⎕DF</tt> applies directly to the object in question and is not automatically applied in a hierarchical fashion.</p><pre>      'X'⎕NS ''
      X
#.X
 
      'Y'X.⎕NS ''
      X.Y
#.X.Y
      X.⎕DF 'This is X'
      X
This is X
 
      X.Y
#.X.Y</pre><p> </p></body>
</html>