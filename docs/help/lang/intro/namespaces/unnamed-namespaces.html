<!doctype html>
<html>
<head><meta charset="utf-8"><title></title><link rel="stylesheet" href="../../../../help.css"></head>
<body><h3>Unnamed Namespaces </h3><p>The monadic form of <tt>⎕NS</tt> makes a new (and unique) unnamed namespace and returns a ref to it.</p><p>One use of unnamed namespaces is to represent hierarchical data structures; for example, a simple employee database:</p><p>The first record is represented by <tt>JOHN</tt> which is a ref to an <i>unnamed</i> namespace:</p><pre>      JOHN←⎕NS ''                         
      JOHN
#.[Namespace]
 
      JOHN.FirstName←'John'
      JOHN.FirstName
John
 
      JOHN.LastName←'Smith'
      JOHN.Age←50</pre><p>Data variables for the second record, <tt>PAUL</tt>, can be established using strand, or vector, assignment:</p><pre>      PAUL←⎕NS ''
      PAUL.(FirstName LastName Age←'Paul' 'Brown' 44)</pre><p>The function <tt>SHOW</tt> can be used to display the data in each record (the function is split into 2 lines only to fit on the printed page). Notice that its argument is a ref.</p><pre>     ∇ R←SHOW PERSON
[1]    R←PERSON.FirstName,' ',PERSON.LastName
[2]    R, ←' is ',⍕PERSON.Age
     ∇
 
      SHOW JOHN
John Smith is 50
 
      SHOW PAUL
Paul Brown is 44</pre><p>An alternative version of the function illustrates the use of the <tt>:With  :EndWith</tt> control structure to execute an expression, or block of expressions, within a namespace:</p><pre>     ∇ R←SHOW1 PERSON
[1]    :With PERSON
[2]        R←FirstName,' ',LastName,' is ',(⍕Age)
[3]    :EndWith
     ∇
 
      SHOW1 JOHN
John Smith is 50</pre><p>In this case, as only a single expression is involved, it can be expressed more simply using parentheses.</p><pre>     ∇ R←SHOW2 PERSON
[1]    R←PERSON.(FirstName,' ',LastName,' is ',(⍕Age))
     ∇
      SHOW2 PAUL
Paul Brown is 44</pre><p>Dynamic functions also accept refs as arguments:</p><pre>      SHOW3←{
        ⍵.(FirstName,' ',LastName,' is ',⍕Age)
      }
 
      SHOW3 JOHN
John Smith is 50</pre></body>
</html>